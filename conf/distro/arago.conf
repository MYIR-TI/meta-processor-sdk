# Name and version of the distribution
DISTRO_NAME = "Arago"
DISTRO_VERSION = "2015.04"
DISTRO_FEED_URI ?= "http://lcpd.dal.design.ti.com"
FEEDURIPREFIX ?= "core-sdk/nightly/deploy/ipk/"

# SDK paths and names
SDK_NAME_PREFIX = "arago"
SDK_VERSION = "${DISTRO_VERSION}"
SDK_VENDOR = "-${SDK_NAME_PREFIX}"
SDK_NAME = "${SDK_NAME_PREFIX}-${SDK_VERSION}"
SDKPATH_REAL = "/usr/local/${SDK_NAME_PREFIX}-${SDK_VERSION}"
SDKPATH = "${@"/tmp/"+"x"*96+"/"+"y"*96}"

# Mask any broken recipes (currently none)
#BBMASK = ""

# Also build 32bit Pseudo on 64bit machines, fix error when preloading libpseudo.so
NO32LIBS = "0"

# Set our distro features, don't forget all the standard libc ones
DISTRO_FEATURES = "alsa argp bluetooth ext2 irda largefile pcmcia usbgadget usbhost wifi xattr nfs zeroconf pci 3g ipv4 ipv6 pulseaudio ${DISTRO_FEATURES_LIBC}"

# Set additional distro features
DISTRO_FEATURES += "pam ${@base_contains('MACHINE_FEATURES', 'screen', 'wayland', '', d)}"

# Set preferred gstreamer version (0.10 vs 1.0)
GST_PROVIDER ?= "1.0"

# Set preferred Qt version
QT_PROVIDER ?= "qt5"

# Distro-specific package configuration
# Weston - use fbdev compositor and provide launcher
PACKAGECONFIG_pn-weston = "fbdev kms egl launch libinput"

# Configure Qt5 flags (GLES is still required for qtdeclarative and qtwebkit)
PACKAGECONFIG_GL_pn-qtbase = "gles2 linuxfb"
PACKAGECONFIG_DISTRO_pn-qtbase = "icu examples"

# Configure gstreamer dependency based on GST_PROVIDER
PACKAGECONFIG_pn-qtwebkit = "${@base_conditional('GST_PROVIDER', '0.10', 'gstreamer010', 'gstreamer', d)} qtlocation qtmultimedia qtsensors"

# Enable UI and scripting for perf
PERF_FEATURES_ENABLE ?= "perf-scripting perf-tui"

# ARM9 is not supported by the Linaro toolchain so default back to the Arago
# toolchain for ARM9 based SOCs.
TC_SANE_DEFAULT := "${@bb.utils.contains('SOC_FAMILY','omapl138','arago','linaro',d)}"

# Toolchain should be selected by setting TOOLCHAIN_BRAND in local.conf
# Set some sane defaults, in case someone forgets to set them in local.conf
TOOLCHAIN_TYPE ?= "external"
TOOLCHAIN_BRAND ?= "${@[d.getVar('TC_SANE_DEFAULT', True),'gcc'][d.getVar('TOOLCHAIN_TYPE', True) == 'internal']}"

require conf/distro/include/toolchain-${TOOLCHAIN_BRAND}.inc
require conf/distro/include/toolchain-${TOOLCHAIN_TYPE}.inc

# Enable basic stack and buffer overflow protections
#TARGET_CPPFLAGS += "-fstack-protector -D_FORTIFY_SOURCE=1"
TARGET_CPPFLAGS += "-fstack-protector"

# Load default preferences
require conf/distro/include/arago-prefs.inc

# Remove the ASSUME_PROVIDED setting for git-native so that we can
# use the latest version for the sourceipk which leads to a smaller
# git history.
ASSUME_PROVIDED := "${@filter_out('git-native','${ASSUME_PROVIDED}',d)}"

# Allow branding on top of Arago Distro and Core TI-SDK
# If ARAGO_BRAND is not set in local.conf, default to core
ARAGO_BRAND ??= "core"

require conf/distro/include/branding-${ARAGO_BRAND}.inc

# Disable polkit's systemd configuration from meta-oe
POLKITAUTH = ""

# Enable generation of source packages for SDKs
require conf/distro/arago-source-ipk.conf

# Set ENTERPRISE_DISTRO flag to ensure that recipes that have been flagged
# do not attempt to enable features that have potential commercial
# licensing issues.
ENTERPRISE_DISTRO = "1"

# oe-core uses external libav instead of internal libav for gst-ffmpeg. We
# disable by default the codecs in libav that causes issues with shipping an
# Arago based SDK. No need to prevent gst-ffmpeg from being used since we
# disable troublesome software.
LICENSE_FLAGS_WHITELIST = "commercial_gst-ffmpeg commercial_gstreamer1.0-libav"
